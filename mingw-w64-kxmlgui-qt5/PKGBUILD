# Maintainer (KF5): Anthony Campbell <anthony@claydonkey.com>

_variant=-${KF5_VARIANT:-shared}
_stripped=${KF5_STRIP:strip}
_releasetype=${KF5_RELEASE:!debug}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kxmlgui"
pkgver=5.37.0
pkgrel=1
arch=('x86_64')
pkgdesc="Framework for managing menu and toolbar actions (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" "${MINGW_PACKAGE_PREFIX}-bzip2")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-kglobalaccel-qt5" "${MINGW_PACKAGE_PREFIX}-ktextwidgets-qt5" "${MINGW_PACKAGE_PREFIX}-attica-qt5" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}" 
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz"
"0001-CMakePolicy.patch" "0002-CMakePolicy.patch" "0003-CMakePolicy.patch" "0004-CMakeList.patch")
sha256sums=('e235367a31a38c1c114032123b13e7954a93a8e1423eb1829176a6373b953b4b'
            '416327a06b9d64eb2b211a121a6e0c9655bf2b728a8edaa60a3b412f3f53f163'
            '24d6ecb250e568250437176136f3d583b52504ef3e10e70e94f6951d098c55a3'
            '0a1a7ee3a1d661ded123a1c226e4e7571145c9c72cf3fe3aac7c44bb973fe558'
            'a6735f426dab599b7f7fcbd22d4a49142196827194956a92f132fce56d45ae48')
options=(${_stripped} ${_releasetype})

prepare() {
  mkdir -p build-${CARCH}${_variant}
#  patch -Np1 -i ${srcdir}/0001-CMakePolicy.patch
#  patch -Np1 -i ${srcdir}/0002-CMakePolicy.patch
#  patch -Np1 -i ${srcdir}/0003-CMakePolicy.patch
  patch -Np1 -i ${srcdir}/0004-CMakeList.patch
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX}/share/qt5/mkspecs \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}

