# Maintainer: Anthony Campbell <anthony@claydonkey.com>


_realname=gpgme
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.0
pkgrel=2
pkgdesc="A C wrapper library for GnuPG (mingw-w64)"
arch=('any')
url="https://gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gnupg"
         "${MINGW_PACKAGE_PREFIX}-libassuan"
         "${MINGW_PACKAGE_PREFIX}-libgpg-error"
         "${MINGW_PACKAGE_PREFIX}-npth"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         )
options=('!libtool' 'strip' '!debug')
source=("https://www.gnupg.org/ftp/gcrypt/${_realname}/${_realname}-${pkgver}.tar.bz2"{,.sig}
        0001-qtnodocsnotests.patch
        0002-GpgMeppConfigFix.patch
        0003-QGpgMeConfigFix.patch
        0004-gpgme-find-gnupg.patch
  )
#These might be signed by any of these keys https://gnupg.org/signature_key.html
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
sha256sums=('1b29fedb8bfad775e70eafac5b0590621683b2d9869db994568e6401f4034ceb'
            'SKIP'
            'a5c105d53e0c5b1d69605a89538179b632785c74e8bf554440c7565ea4039842'
            '88bac22f499d40eb987128594cd76e52b7ce06a887da633c8b8070cf4fcc1cb8'
            '5cddaead93d2251a2800cd9591409f10f0cac877ccbdb488759dd01bf94b01f4'
            '31725bb1a2d5d6901e9e5f4ca143b5435f6614299801a78577c96429253d8d3b')

prepare() {
   patch -Np1  -i "${srcdir}"/0001-qtnodocsnotests.patch
   patch -Np1  -i "${srcdir}"/0002-GpgMeppConfigFix.patch
   patch -Np1  -i "${srcdir}"/0003-QGpgMeConfigFix.patch
#   patch -Np1  -i "${srcdir}"/0004-gpgme-find-gnupg.patch
   cd "${srcdir}"/${_realname}-${pkgver}
   autoreconf -ivf
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}


  "${srcdir}"/${_realname}-${pkgver}/configure \
    --disable-fd-passing \
    --enable-shared \
    --enable-static \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-gnu-ld \
    --disable-gpg-test \
    --disable-gpgsm-test \
    --libexecdir=${MINGW_PREFIX}/bin 
  make
}

check() {
  cd "${srcdir}"/build-${MINGW_CHOST}
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
