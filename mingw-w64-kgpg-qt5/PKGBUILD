# Maintainer (KF5): Anthony Campbell <anthony@claydonkey.com>

pkgver=17.08.1
pkgrel=1
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5

_kde_f5_init_package "${_variant}" "kgpg"
pkgdesc="KDE  cryptographic ui"
arch=('any')
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=()
makedepends=(${MINGW_PACKAGE_PREFIX}-boost python )
#:_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}" "${MINGW_PACKAGE_PREFIX}-kitemmodels-qt5"
#conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$_basename-$pkgver.tar.xz"{,.sig})
sha256sums=('9efd467e510f1bce73bbeeba3764fb677aebc2551d2a078208720947f64f8fc1'
            'SKIP')

validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>
options=(${_stripped} ${_releasetype})

prepare() {
  mkdir -p build-${CARCH}${_variant}
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX}/share/qt5/mkspecs \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}
package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="$pkgdir" install
}
