# Maintainer (KF5): Anthony Campbell <anthony@claydonkey.com>

_variant=-${KF5_VARIANT:-shared}
_stripped=${KF5_STRIP:strip}
_releasetype=${KF5_RELEASE:!debug}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kdelibs4support"
pkgver=5.37.0
pkgrel=1
pkgdesc='Legacy support for kdelibs 4 compatibility'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=( )
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" )
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-kparts-qt5"  "${MINGW_PACKAGE_PREFIX}-kdesignerplugin-qt5"  "${MINGW_PACKAGE_PREFIX}-kunitconversion-qt5"  
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${_basename}-${pkgver}.tar.xz" "0001-CMakeTargetInclude.patch")
sha256sums=('09d2a7817dbfd0aec82886fb2c5a83321d8b535a438013ed78d5611d2a00e1ff'
            '9d4641db9eacc11adc554a7cf5041544994e749a2f229d7f96289464dd027cc9')
options=(${_stripped} ${_releasetype})

prepare() {
 mkdir -p build-${CARCH}${_variant}
# patch -Np1 -i 0001-CMakeTargetInclude.patch
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX}/share/qt5/mkspecs \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
make
}

package() {
msg2 "PKGDIR=${pkgdir}"  
cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
